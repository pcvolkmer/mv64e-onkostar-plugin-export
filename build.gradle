plugins {
    id 'java-library'
    id("com.gradleup.shadow") version "8.3.8"
    id("com.diffplug.spotless") version "7.2.1"
}

group = 'dev.pcvolkmer.onco'
version = '0.2.0' // x-release-please-version
description = 'OS Export-Plugin f√ºr das DNPM-Datenmodell 2.1'

java.sourceCompatibility = JavaVersion.VERSION_11

repositories {
    mavenCentral()
    // Ignore the next two maven repositories if you want to use a local build
    maven {
        url = uri("https://git.dnpm.dev/api/packages/public-snapshots/maven")
    }
    maven {
        url = uri("https://git.dnpm.dev/api/packages/public/maven")
    }
    // Local repository for local dependency build
    mavenLocal()
}

ext {
    onkostarVersion = '2.14.2'
    springVersion = '4.3.8.RELEASE'
    slf4jVersion = '1.7.2'
    junitVersion = '5.13.3'
    assertjVersion = '3.27.3'
    mockitoVersion = '5.18.0'
}

dependencies {
    implementation files("./libs/onkostar-api-${onkostarVersion}.jar")

    implementation "org.springframework:spring-beans:${springVersion}"
    implementation "org.springframework:spring-context:${springVersion}"
    implementation "org.springframework:spring-web:${springVersion}"
    implementation "org.springframework:spring-jdbc:${springVersion}"
    implementation "org.slf4j:slf4j-api:${slf4jVersion}"

    implementation("dev.pcvolkmer.mv64e:mv64e-onkostar-data:0.3.0")

    testRuntimeOnly 'org.junit.platform:junit-platform-launcher:1.13.3'

    testImplementation "org.springframework:spring-test:${springVersion}"
    testImplementation "org.junit.jupiter:junit-jupiter-engine:${junitVersion}"
    testImplementation "org.junit.jupiter:junit-jupiter-params:${junitVersion}"
    testImplementation "org.assertj:assertj-core:${assertjVersion}"
    testImplementation "org.mockito:mockito-core:${mockitoVersion}"
    testImplementation "org.mockito:mockito-junit-jupiter:${mockitoVersion}"
    testImplementation "ca.uhn.hapi:hapi-structures-v26:2.3"
    testImplementation "org.hibernate:hibernate-core:4.3.11.Final"
}

tasks.named('test') {
    useJUnitPlatform()
    dependsOn(tasks.spotlessCheck)
}

// Include dependencies in resulting JAR file
tasks.shadowJar {
    minimize()
    dependencies {
        exclude("de/itc/onkostar/api/**")
        include(dependency("dev.pcvolkmer.mv64e:mv64e-onkostar-data"))
        include(dependency("dev.pcvolkmer.mv64e:mtb-dto"))
    }
}

spotless {
    java {
        importOrder()
        removeUnusedImports()
        googleJavaFormat()
    }
}
